group 'cl.uc.psanabria.sysrec'
version '0.1'

apply plugin: 'application'

sourceCompatibility = "1.7"
mainClassName = "cl.uc.psanabria.sysrec.work1.Main"
applicationDefaultJvmArgs = ['-splash:images/splash.gif',
                            '-DdataPath=data',
                             '-Dswing.defaultlaf=javax.swing.plaf.nimbus.NimbusLookAndFeel']

repositories {
    mavenCentral()
}

startScripts {
    doLast {
        windowsScript.text = windowsScript.text
                .replace('-splash:', '-splash:%APP_HOME%/')
                .replace('-DdataPath=', '-DdataPath=%APP_HOME%/' )
        unixScript.text = unixScript.text
                .replace('-splash:', '-splash:$APP_HOME/')
                .replace('-DdataPath=', '-DdataPath=$APP_HOME/' )
    }
}

distributions {
    main {
        contents {
            into("data") {
                from 'data'
            }

            into("images") {
                from 'images'
            }
        }
    }
}

dependencies {
    testCompile 'junit:junit:4.11'
    compile 'org.grouplens.lenskit:lenskit-all:2.2.1'
    compile 'org.slf4j:slf4j-jdk14:1.7.21'
    compile 'de.erichseifert.gral:gral-core:0.11'
}
